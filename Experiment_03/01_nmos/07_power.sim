* nmos load inverter with varying width of load

.include ./t14y_tsmc_025_level3.txt

*** netlist
mu vdd vdd vout 0 cmosn l = 9u w = 3u
md vout vin 0 0 cmosn l = 3u w = 9u
c_load vout 0 .1f

vdd vdd 0 5
vin vin 0 PULSE(5 0 0 0 0 2n 4n)

.control

	foreach cap .01f .1f 1f
		alter @c_load[capacitance] $cap
		tran .01n 4n

		meas tran vmax max vout from=.5n to=1.5n
		meas tran vmin min vout from=2.5n to=3.5n

		let v10 = Vmin + 0.1*(Vmax - Vmin)
		let v50 = Vmin + 0.5*(Vmax - Vmin)
		let v90 = Vmin + 0.9*(Vmax - Vmin)

		print @c_load[capacitance]
		print v10, v50, v90

		meas tran trise trig vout val=v10 rise=1 targ vout val=v90 rise=1
		print trise
		meas tran tfall trig vout val=v90 fall=1 targ vout val=v10 fall=1
		print tfall

		meas tran tphl trig vin val=2.5 rise = 1 targ vout val=v50 fall=1
		print tphl

		meas tran tplh trig vin val=2.5 fall=1 targ vout val=v50 rise=1
		print tplh

		let tp = (tphl+tplh)/2
		print tp

		print i(vdd) title "Current with load = $cap"
		plot vin vout title "Transfer function with load = $cap"
		plot vdd*i(vdd) title "Power consumed with load = $cap"


		dc vin 0 5 0.1
		plot vout title "DC capponse with load = $cap"

		plot vin - vout title "Operation with load = $cap"

		echo "Dynamic power"
		print $(cap*25)/4n

		let p = vdd*i(vdd)
		meas tran power avg p from=0n to=4n



	end

.endc

.end

