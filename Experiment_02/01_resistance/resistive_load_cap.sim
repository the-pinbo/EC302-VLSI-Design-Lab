* capistive load inverter with a capacitive load


.include ./t14y_tsmc_025_level3.txt

*** netlist
md vout vin 0 0 cmosn l = 1u w = 10u
r_load vdd vout 50k
c_load vout 0 .01u
r_out vout 0 1000k

vdd vdd 0 5
vin vin 0 PULSE(0 5 0 0 0 2n 4n)

.control

	foreach cap .01f .1f 1f
		alter @c_load[capacitance] $cap
		tran .01n 4n

		meas tran vmax max vout from=0 to=2n
		meas tran vmin min vout from=0 to=2n

		let v10 = Vmin + 0.1*(Vmax - Vmin)
		let v50 = Vmin + 0.5*(Vmax - Vmin)
		let v90 = Vmin + 0.9*(Vmax - Vmin)

		print @c_load[capacitance]
		print v10, v50, v90

		meas tran trise trig vout val=v10 rise=1 targ vout val=v90 rise=1
		print trise
		meas tran tfall trig vout val=v90 fall=1 targ vout val=v10 fall=1
		print tfall

		meas tran tphl trig vin val=2.5 rise = 1 targ vout val=v50 fall=1
		print tphl

		meas tran tplh trig vin val=2.5 fall=1 targ vout val=v50 rise=1
		print tplh

		let tp = (tphl+tplh)/2
		print tp

		print i(vdd) title "Current with load = $cap"
		plot vin vout title "Transfer function with load = $cap"
		plot vdd*i(vdd) title "Power consumed with load = $cap"


		dc vin 0 5 0.1
		plot vout title "DC capponse with load = $cap"

		plot vin - vout title "Operation with load = $cap"

	end

.endc

.end
